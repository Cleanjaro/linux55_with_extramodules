# Based on the file created for Arch Linux by:
# Ray Rashif <schiv@archlinux.org>
# Mateusz Herych <heniekk@gmail.com>
# Charles Lindsay <charles@chaoslizard.org>

# Maintainer: David Spink <yorper_protonmail.com>

_linuxprefix=linux55
_extramodules=extramodules-5.5-CLEANJARO
pkgname=$_linuxprefix-vhba-module
_pkgname=vhba-module
pkgver=20190831
pkgrel=4
pkgdesc="Kernel module that emulates SCSI devices"
arch=('x86_64')
url="http://cdemu.sourceforge.net/"
license=('GPL')
depends=("$_linuxprefix")
makedepends=("$_linuxprefix-headers")
provides=("$_pkgname=$pkgver")
groups=("$_linuxprefix-extramodules")
install=$_pkgname.install
source=("http://downloads.sourceforge.net/cdemu/$_pkgname-$pkgver.tar.bz2"
        '60-vhba.rules')
sha256sums=('9a83772cf8bec15723f7a8105a0aae3faa057edbd5595e6c081e5803a8996bf9'
            '3052cb1cadbdf4bfb0b588bb8ed80691940d8dd63dc5502943d597eaf9f40c3b')

prepare() {
  cd "$srcdir/$_pkgname-$pkgver"
}

build() {
  _kernver="$(cat /usr/lib/modules/$_extramodules/version)"
  
  cd "$srcdir/$_pkgname-$pkgver"
  make -j1 KDIR=/usr/lib/modules/${_kernver}/build
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  install -D vhba.ko "$pkgdir/usr/lib/modules/$_extramodules/vhba.ko"

  sed -i "s/EXTRAMODULES=.*/EXTRAMODULES=$_extramodules/" \
    "$startdir/vhba-module.install"

  find "$pkgdir" -name '*.ko' -exec gzip -9 {} \;

  install -Dm644 "$srcdir/60-vhba.rules" \
    "$pkgdir/usr/lib/udev/rules.d/60-$_linuxprefix-vhba.rules"
}
